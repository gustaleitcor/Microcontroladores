;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*              MODIFICAÃ‡Ã•ES PARA USO COM 12F675                   *
;*                FEITAS PELO PROF. MARDSON                        *
;*                      DEZEMBRO DE 2024                           *
;*                 BASEADO NO EXEMPLO DO LIVRO                     *
;*           Desbravando o PIC. David JosÃ© de Souza                *
;*-----------------------------------------------------------------*
;*   MODELO PARA O PIC 12F675                                      *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                     ARQUIVOS DE DEFINIÃ‡Ã•ES                      *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
#INCLUDE <p12f675.inc>	;ARQUIVO PADRÃƒO MICROCHIP PARA 12F675

	__CONFIG _BODEN_OFF & _CP_OFF & _PWRTE_ON & _WDT_ON & _MCLRE_ON & _INTRC_OSC_NOCLKOUT

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                    PAGINAÃ‡ÃƒO DE MEMÃ“RIA                         *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;DEFINIÃ‡ÃƒO DE COMANDOS DE USUÃRIO PARA ALTERAÃ‡ÃƒO DA PÃGINA DE MEMÃ“RIA
#DEFINE	BANK0	BCF STATUS,RP0	;SETA BANK 0 DE MEMÃ“RIA
#DEFINE	BANK1	BSF STATUS,RP0	;SETA BANK 1 DE MAMÃ“RIA

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                         VARIÃVEIS                               *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; DEFINIÃ‡ÃƒO DOS NOMES E ENDEREÃ‡OS DE TODAS AS VARIÃVEIS UTILIZADAS 
; PELO SISTEMA

	CBLOCK	0x20	;ENDEREÃ‡O INICIAL DA MEMÃ“RIA DE
					;USUÃRIO
		W_TEMP		;REGISTRADORES TEMPORÃRIOS PARA USO
		STATUS_TEMP	;JUNTO Ã€S INTERRUPÃ‡Ã•ES

		;COLOQUE AQUI SUAS NOVAS VARIÃVEIS
		;NÃƒO ESQUEÃ‡A COMENTÃRIOS ESCLARECEDORES

	ENDC			;FIM DO BLOCO DE DEFINIÃ‡ÃƒO DE VARIÃVEIS

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                        FLAGS INTERNOS                           *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; DEFINIÃ‡ÃƒO DE TODOS OS FLAGS UTILIZADOS PELO SISTEMA

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                         CONSTANTES                              *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; DEFINIÃ‡ÃƒO DE TODAS AS CONSTANTES UTILIZADAS PELO SISTEMA

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                           ENTRADAS                              *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; DEFINIÃ‡ÃƒO DE TODOS OS PINOS QUE SERÃƒO UTILIZADOS COMO ENTRADA
; RECOMENDAMOS TAMBÃ‰M COMENTAR O SIGNIFICADO DE SEUS ESTADOS (0 E 1)

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                           SAÃDAS                                *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; DEFINIÃ‡ÃƒO DE TODOS OS PINOS QUE SERÃƒO UTILIZADOS COMO SAÃDA
; RECOMENDAMOS TAMBÃ‰M COMENTAR O SIGNIFICADO DE SEUS ESTADOS (0 E 1)

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                       VETOR DE RESET                            *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

	ORG	0x00			;ENDEREÃ‡O INICIAL DE PROCESSAMENTO
	GOTO	INICIO
	
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                    INÃCIO DA INTERRUPÃ‡ÃƒO                        *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; ENDEREÃ‡O DE DESVIO DAS INTERRUPÃ‡Ã•ES. A PRIMEIRA TAREFA Ã‰ SALVAR OS
; VALORES DE "W" E "STATUS" PARA RECUPERAÃ‡ÃƒO FUTURA

	ORG	0x04			;ENDEREÃ‡O INICIAL DA INTERRUPÃ‡ÃƒO
	MOVWF	W_TEMP		;COPIA W PARA W_TEMP
	SWAPF	STATUS,W
	MOVWF	STATUS_TEMP	;COPIA STATUS PARA STATUS_TEMP

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                    ROTINA DE INTERRUPÃ‡ÃƒO                        *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; AQUI SERÃƒO ESCRITAS AS ROTINAS DE RECONHECIMENTO E TRATAMENTO DAS
; INTERRUPÃ‡Ã•ES

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                 ROTINA DE SAÃDA DA INTERRUPÃ‡ÃƒO                  *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; OS VALORES DE "W" E "STATUS" DEVEM SER RECUPERADOS ANTES DE 
; RETORNAR DA INTERRUPÃ‡ÃƒO

SAI_INT
	SWAPF	STATUS_TEMP,W
	MOVWF	STATUS		;MOVE STATUS_TEMP PARA STATUS
	SWAPF	W_TEMP,F
	SWAPF	W_TEMP,W	;MOVE W_TEMP PARA W
	RETFIE

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*	            	 ROTINAS E SUBROTINAS                      *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
; CADA ROTINA OU SUBROTINA DEVE POSSUIR A DESCRIÃ‡ÃƒO DE FUNCIONAMENTO
; E UM NOME COERENTE Ã€S SUAS FUNÃ‡Ã•ES.

SUBROTINA1

	;CORPO DA ROTINA

	RETURN

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                     INICIO DO PROGRAMA                          *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
	
INICIO
	BANK1				;ALTERA PARA O BANCO 1
	MOVLW	B'00000001' ;CONFIGURA TODAS AS PORTAS DO GPIO (PINOS)
	MOVWF	TRISIO		;COMO SAÃDAS
	MOVLW	B'00010001'
	MOVWF	ANSEL 		;DEFINE PORTAS COMO Digital I/O
	MOVLW	B'00001111'
	MOVWF	OPTION_REG	;DEFINE OPÃ‡Ã•ES DE OPERAÃ‡ÃƒO
	MOVLW	B'00000000'
	MOVWF	INTCON		;DEFINE OPÃ‡Ã•ES DE INTERRUPÃ‡Ã•ES
	CALL	0x3FF
	BANK0				;RETORNA PARA O BANCO
	MOVLW	B'00000111'
	MOVWF	CMCON		;DEFINE O MODO DE OPERAÃ‡ÃƒO DO COMPARADOR ANALÃ“GICO
	MOVLW	B'00000001'
	MOVWF	ADCON0
	CLRF	T1CON

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                     INICIALIZAÃ‡ÃƒO DAS VARIÃVEIS                 *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                     ROTINA PRINCIPAL                            *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

;===============================================================================
; CARACTERÍSTICAS DE CONSUMO:
;   - Corrente no modo WDT:		17 µA  (~2 segundos em atividade)
;   - Corrente durante conversão A\D:	1  µA  (~41 µs por conversão)
;   - Corrente com LED ativo:		18 mA  (~2 segundos por ativação)
;   - Consumo total estimado:		90.850 µWh
;===============================================================================
	
MAIN
    BSF ADCON0, ADON	    ; Liga o módulo ADC (Conversor Analógico-Digital)
    CLRWDT		    ; Limpa o Watchdog Timer (evita reset acidental)

    BANK1		    
    MOVLW B'00001111'	    ; Carrega W com o valor para configurar OPTION_REG:
			    ; - Bits 0-2: Prescaler do WDT (1:128)
			    ; - Bit 3: Prescaler designado ao WDT
    MOVWF OPTION_REG	    ; Configura o OPTION_REG com os valores acima
    BANK0		    

    BSF ADCON0, GO	    ; Inicia a conversão ADC (seta o bit GO)

CONV_LOOP
    BTFSS PIR1, ADIF	    ; Verifica se a conversão ADC terminou (flag ADIF setada)
    GOTO CONV_LOOP	    ; Se não terminou, continua no loop
    BCF PIR1, ADIF	    ; Limpa a flag de interrupção do ADC

MENOR_QUE_DOIS		    ; Verifica se a tensão é menor que ~2V (102 em ADRESH)
    MOVLW .102		    ; Carrega W com o valor de referência (102 ? 2V)
    SUBWF ADRESH, W	    ; Subtrai W do resultado do ADC (byte alto)
    BTFSC STATUS, C	    ; Verifica a flag de carry (se ADRESH >= W)
    GOTO MENOR_QUE_TRES	    ; Se >= 102, pula para o próximo limite

    ; Se a tensão está abaixo de ~2V
    BCF GPIO, GP2	    ; Desliga GP2
    BCF GPIO, GP4	    ; Desliga GP4
    BCF GPIO, GP5	    ; Desliga GP5
    BSF GPIO, GP1	    ; Liga GP1 (indica faixa de tensão mais baixa)
    GOTO DORMIR		    ; Vai para o modo de dormir

MENOR_QUE_TRES		    ; Verifica se a tensão é menor que ~3V (153 em ADRESH)
    MOVLW .153		    ; Carrega W com o valor de referência (153 ? 3V)
    SUBWF ADRESH, W	    ; Subtrai do resultado do ADC
    BTFSC STATUS, C	    ; Verifica a flag de carry
    GOTO MENOR_QUE_QUATRO   ; Se >= 153, pula para o próximo limite

    ; Se a tensão está entre ~2V e ~3V
    BCF GPIO, GP1	    ; Desliga GP1
    BCF GPIO, GP4	    ; Desliga GP4
    BCF GPIO, GP5	    ; Desliga GP5
    BSF GPIO, GP2	    ; Liga GP2 (indica faixa de tensão média-baixa)
    GOTO DORMIR		    ; Vai para o modo de dormir

MENOR_QUE_QUATRO	    ; Verifica se a tensão é menor que ~4V (204 em ADRESH)
    MOVLW .204		    ; Carrega W com o valor de referência (204 ? 4V)
    SUBWF ADRESH, W	    ; Subtrai do resultado do ADC
    BTFSC STATUS, C	    ; Verifica a flag de carry
    GOTO MAIOR_QUE_QUATRO   ; Se >= 204, pula para a faixa mais alta

    ; Se a tensão está entre ~3V e ~4V
    BCF GPIO, GP1	    ; Desliga GP1
    BCF GPIO, GP2	    ; Desliga GP2
    BCF GPIO, GP5	    ; Desliga GP5
    BSF GPIO, GP4	    ; Liga GP4 (indica faixa de tensão média-alta)
    GOTO DORMIR		    ; Vai para o modo de dormir

MAIOR_QUE_QUATRO	    ; Tensão maior que ~4V (faixa mais alta)
    BCF GPIO, GP1	    ; Desliga GP1
    BCF GPIO, GP2	    ; Desliga GP2
    BCF GPIO, GP4	    ; Desliga GP4
    BSF GPIO, GP5	    ; Liga GP5 (indica faixa de tensão mais alta)

DORMIR			    ; Rotina de "dormir" (baixo consumo)
    BCF ADCON0, ADON	    ; Desliga o ADC para economizar energia
    SLEEP		    ; Entra em modo de baixo consumo (SLEEP)
    GOTO MAIN		    ; Ao acordar, reinicia o programa

;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*                       FIM DO PROGRAMA                           *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

	END
